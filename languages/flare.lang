ws <- space+

comment <- "#" (!"\n" .)*

range  <- '[' (range_escape / (!']' .))* ']'?
range_escape <- cap{'\\' [\-\[\]\\], "constant.string.escape"}
sq_str <- "'" (group &"'" / (str_escape / (!"'" .))*) "'"?
dq_str <- '"' (group &'"' / (str_escape / (!'"' .))*) '"'?
str_escape <- cap{'\\' ['"tnbfr\\], "constant.string.escape"}
string <- cap{range / sq_str / dq_str, "constant.string"}

group <- cap{words{"keyword"}, "keyword"}
       / cap{words{"type"}, "type"}
       / cap{words{"default"}, "default"}
       / cap{words{"comment"}, "comment"}
       / cap{words{"constant.string.char"}, "constant.string.char"}
       / cap{words{"constant.string.escape"}, "constant.string.escape"}
       / cap{words{"constant.number"}, "constant.number"}
       / cap{words{"constant.string"}, "constant.string"}
       / cap{words{"constant"}, "constant"}
       / cap{words{"symbol.tag"}, "symbol.tag"}
       / cap{words{"preproc"}, "preproc"}
       / cap{words{"function"}, "function"}
       / cap{words{"special"}, "special"}
       / cap{words{"underlined"}, "underlined"}
       / cap{words{"error"}, "error"}
       / cap{words{"todo"}, "todo"}

function <- cap{words{
    "cap", "words", "include", "ref", "back"
}, "function"}

operator <- cap{"<-" / [*?/.!&+\-], "symbol.operator"}

type <- cap{word &(space* "<-"), "type"}

token <- ws
       / function
       / type
       / string
       / comment
       / operator
